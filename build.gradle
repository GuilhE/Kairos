buildscript {
    ext.coroutines_version = '1.4.2'
    apply from: './dependencies.gradle'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.gradle_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$versions.hilt_core_version"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.7.0'
}

subprojects {
    repositories {
        google()
        mavenCentral()
        //noinspection JcenterRepositoryObsolete
        jcenter {
            content {
                includeModule("org.jetbrains.kotlinx", "kotlinx-collections-immutable-jvm")
            }
        }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint("0.40.0")
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }
}